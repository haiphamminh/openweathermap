buildscript {
    ext {
        springBootVersion = '2.1.9.RELEASE'
        commonDependencies = [
                "lombok"            : "org.projectlombok:lombok:1.18.10",
                "commonsIO"         : "org.apache.commons:commons-io:1.3.2",
                "jackonDatabind"    : "com.fasterxml.jackson.core:jackson-databind:2.10.0",
                "mapstruct"         : "org.mapstruct:mapstruct:1.3.1.Final",
                "mapstructProcessor": "org.mapstruct:mapstruct-processor:1.3.1.Final",
                "jupiter"           : "org.junit.jupiter:junit-jupiter:5.5.1"
        ]
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

subprojects {
    if (it.hasProperty("type")) {
        if (it.properties.get("type") == "java-boot-application") {
            apply plugin: 'org.springframework.boot'
        }
    }

    group = 'org.openweathermap'
    version = '1.0'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        compile commonDependencies.commonsIO
        compile commonDependencies.jackonDatabind
        compileOnly commonDependencies.lombok
        annotationProcessor commonDependencies.lombok
        compileOnly commonDependencies.mapstruct
        annotationProcessor commonDependencies.mapstructProcessor
        testCompile commonDependencies.jupiter
    }

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
        repositories {
            mavenLocal()
        }
    }
}